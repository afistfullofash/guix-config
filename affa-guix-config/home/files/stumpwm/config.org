#+TITLE: StumpWM Config
#+PROPERTY: header-args:lisp :tangle init.lisp :exports both :eval never
In general we are not allowing any variable setting or function calls outside of a defun. The only exected exception to this is the inital load underneath this and the finishing actions section which runs all the init-* defuns.
* Initial Config
Start slynk for debugging purposes first and then load in our required stumpwm contrib modules
  #+begin_src lisp
    (in-package :stumpwm-user)
    (asdf:load-system :slynk)
    (slynk:create-server :port 1337
    		     :dont-close t)

    (setf *startup-message* (format nil "Welcome Natalie!"))

    (asdf:load-system "local-time")
    (asdf:load-system "pamixer")
    (asdf:load-system "battery-portable")
  #+end_src
* Visual
** Themeing
This is a quick themeing system for stumpwm with a couple of theme options.

*** General Functions
#+begin_src lisp
  (defvar *theme-type-env-name* "AFFOA_SYSTEM_THEME_TYPE")

  (defun set-system-themeing ()
    (let ((system-theme (getenv *theme-type-env-name*)))
      (if (equal "dark" system-theme)
  	(apply-theme 'dracula)
  	(apply-theme 'gruvbox-light)))
    (set-bar-med-color)
    (set-bar-hi-color)
    (set-bar-crit-color))

  (defun toggle-system-themeing ()
    (if (equal (getenv *theme-type-env-name*) "dark")
        (setf (getenv *theme-type-env-name*) "light")
        (setf (getenv *theme-type-env-name*) "dark"))
    (set-system-themeing)
    (toggle-modeline-all-screens)
    (toggle-modeline-all-screens)
    (stumpwm:message "Remember to restart emacs"))
#+end_src
*** Basic StumpWM Themeing Sytem 
This gives us some very general functions to generate and apply /themes/ with.
    
    #+begin_src lisp
      ;;; Visual
      (defvar *themes* (make-hash-table))
      (defvar *current-theme* 'default)

      (defun add-theme (name theme)
        (setf (gethash name *themes*) theme))

      ;;; Colors based off spacemacs-dark-theme for emacs
      (defclass theme ()
        ((fg
          :initarg :fg
          :initform "White"
          :type string)
         (bg
          :initarg :bg
          :initform "Black"
          :type string)

         (border
          :initarg :border
          :initform "White"
          :type string)

         (focus
          :initarg :focus
          :initform "White"
          :type string)
         (unfocus
          :initarg :unfocus
          :initform "Black"
          :type string)

         (mode-line-fg
          :initarg :mode-line-fg
          :initform *mode-line-foreground-color*
          :type string)
         (mode-line-bg
          :initarg :mode-line-bg
          :initform *mode-line-background-color*
          :type string)
         (mode-line-border
          :initarg :mode-line-border
          :initform *mode-line-border-color*
          :type string)
         
         ;; These are the default colors of *colors* in order
         (black
          :initarg :black
          :initform "black"
          :type string)
         (red
          :initarg :red
          :initform "red"
          :type string)
         (green
          :initarg :green
          :initform "green"
          :type string)
         (yellow
          :initarg :yellow
          :initform "yellow"
          :type string)
         (blue
          :initarg :blue
          :initform "blue"
          :type string)
         (magenta
          :initarg :magenta
          :initform "magenta"
          :type string)
         (cyan
          :initarg :cyan
          :initform "cyan"
          :type string)
         (white
          :initarg :white
          :initform "white"
          :type string)
         (custom-one
          :initarg :custom-one
          :initform "black"
          :type string)
         (custom-two
          :initarg :custom-two
          :initform "white"
          :type string)))

      (defun get-theme (theme-name)
        (gethash theme-name *themes*))


      (defun with-current-theme ()
        (get-theme *current-theme*))


      (defun apply-theme-raw (theme)
        (set-fg-color (slot-value theme 'fg))
        (set-bg-color (slot-value theme 'bg))
        (set-border-color (slot-value theme 'border))
        (set-focus-color (slot-value theme 'focus))
        (set-unfocus-color (slot-value theme 'unfocus))

        (setf *mode-line-foreground-color* (slot-value theme 'mode-line-fg)
      	,*mode-line-background-color* (slot-value theme 'mode-line-bg)
      	,*mode-line-border-color* (slot-value theme 'mode-line-border))
        
        (setf *colors* (list 
      		  (slot-value theme 'black)
      		  (slot-value theme 'red)
      		  (slot-value theme 'green)
      		  (slot-value theme 'yellow)
      		  (slot-value theme 'blue)
      		  (slot-value theme 'magenta)
      		  (slot-value theme 'cyan)
      		  (slot-value theme 'white)
      		  (slot-value theme 'custom-one)
      		  (slot-value theme 'custom-two)))
        (update-color-map (current-screen)))

      (defun apply-theme (theme)
        (apply-theme-raw (get-theme 'default))
        (setf *current-theme* 'default)
        (apply-theme-raw (get-theme theme))
        (setf *current-theme* theme))
#+end_src
*** Default
#+begin_src lisp
  (add-theme 'default
  	   (make-instance 'theme))
#+end_src
*** Spacemacs
#+begin_src lisp
  (add-theme 'spacemacs
  	   (let ((grey "#292b2e")
  		 (purple "#5d4d7a"))
  	     (make-instance 'theme
  			    :fg purple
  			    :bg grey
  			    :border purple
  			    :focus purple
  			    :unfocus grey
  			    :mode-line-fg purple
  			    :mode-line-bg grey
  			    :mode-line-border purple
  			    :black grey
  			    :white purple)))

#+end_src
*** Gruvbox
#+begin_src lisp
  (add-theme 'gruvbox
  	   (let ((fg "#ebdbb2")
  		 (bg "#282828")
  		 (border "#665c54"))
      	     (make-instance 'theme
      			    :fg fg
      			    :bg bg
      			    :border border
      			    :focus fg
      			    :unfocus bg
      			    :mode-line-fg fg
      			    :mode-line-bg bg
      			    :mode-line-border border
      			    :black bg
  			    :white fg)))

  (add-theme 'gruvbox-light
  	   (let ((fg "#3c3836")
  		 (fg4 "#7c6f64")
  		 (bg "#fbf1c7")
  		 ;; 124
  		 (red '("#cc241d" "#9d0006"))
  		 ;; 106
  		 (green '("#98971a" "#79740e"))
  		 ;; 172
  		 (yellow '("#d79921" "#b57614"))
  		 ;; 66
  		 (blue '("#458588" "#076678"))
  		 ;; 132
  		 (purple '("#b16286" "#8f3f71"))
  		 ;; 72
  		 (aqua '("#689d6a" "#427b58"))
  		 (orange '("#d65d0e" "#af3a03"))
  		 ;; 243
  		 (gray '("#7c6f64" "#928374"))
  		 ;; Bright is bg1
  		 (bg2 '("#d5c4a1" "#ebdbb2"))
  		 ;; Bright is bg3
  		 (bg4 '("#a89984" "#bdae93")))
      	     (make-instance 'theme
      			    :fg fg
      			    :bg bg
      			    :border (car orange)
      			    :focus fg
      			    :unfocus bg

      			    :mode-line-fg fg
      			    :mode-line-bg bg
      			    :mode-line-border (car orange)
  			    
  			    :black gray
  			    :red red
  			    :green green
  			    :yellow yellow
  			    :blue blue
  			    :magenta purple
  			    :cyan aqua
  			    :white (list fg fg4)
  			    
  			    :custom-one bg2
  			    :custom-two bg4)))
#+end_src
*** Dracula
#+begin_src lisp
  (add-theme 'dracula
       	   (let ((fg '("#F8F8F2" "#f1fa8c"))
       		 (bg '("#282A36" "#44475a"))
       		 (border "#8BE9FD")
       		 (purple '("#BD93F9" "#ff79c6")))
       	     (make-instance 'theme
       			    :fg (car fg)
       			    :bg (car bg)
       			    :border (car purple)
       			    :focus border
       			    :unfocus (car purple)

  			    :mode-line-fg (car purple)
       			    :mode-line-bg (car bg)
       			    :mode-line-border (car purple)

       			    :black  bg
       			    :white  fg
  			    :red "#FF5555"
       			    :green "#50FA7B"
       			    :yellow "#F1FA8C"
       			    :blue "#8BE9FD"
  			    :magenta purple
       			    :cyan "#FF79C6"
       			    :custom-one "#44475A"
       			    :custom-two "#6272A4")))
#+end_src
** Configuration

   
*** Notify
Currently the stumpwm package built from systems/base.scm cannot load the notify package.
So this is chucked into a function while I debug it.
#+begin_src lisp
  (defun start-notify ()
    (asdf:load-system "notify")
    (notify:notify-server-toggle))
#+end_src
*** Mode Line
#+begin_src lisp
  ;; These are bright by default switch to green, yellow and red
  (defun ml-foreground-color (color)
    (if (typep color 'list)
        (format nil "^(:fg \"~A\")" (car color))
      (format nil "^(:fg \"~A\")" color)))

  (defun ml-background-color (color)
    (if (typep color 'list)
        (format nil "^(:bg \"~A\")" (car color))
      (format nil "^(:bg \"~A\")" color)))

  ;; These are used for high, medium and low priority messages
  ;; They should follow becoming shades of the normal backgrounds
  (defun light-bg ()
    "Gets the current brightest background shade"
    (ml-background-color (slot-value (with-current-theme) 'red)))
  (defun medium-bg ()
    "Gets the current medium background shade"
    (ml-background-color (slot-value (with-current-theme) 'custom-one)))
  (defun dark-bg ()
    "Gets the current darkest background shade"
    (ml-background-color (slot-value (with-current-theme) 'custom-two)))


  ;; These are for colored prioity needing colors
  (defun ml-fmt-low-priority-bg ()
    "Gets a Ok Background Shade (normally green)"
    (ml-background-color (slot-value (with-current-theme) 'green)))
  (defun ml-fmt-medium-priority-bg ()
    "Gets a Warning background shade"
    (ml-background-color (slot-value (with-current-theme) 'yellow)))
  (defun ml-fmt-high-priority-bg ()
    "Gets a Alert background shade"
    (ml-background-color (slot-value (with-current-theme) 'red)))

  (defun set-bar-med-color ()
    (setf stumpwm::*bar-med-color*
      	(ml-foreground-color
      	 (slot-value (with-current-theme) 'green))))

  (defun set-bar-hi-color ()
    (setf stumpwm::*bar-hi-color*
      	(ml-foreground-color
      	 (slot-value (with-current-theme) 'yellow))))

  (defun set-bar-crit-color ()
    (setf stumpwm::*bar-crit-color*
      	(ml-foreground-color
      	 (slot-value (with-current-theme) 'red))))

  (defun mode-line-clear-changes ()
    (format nil "^(:fg \"~A\")^(:bg \"~A\")"
    	  (slot-value (with-current-theme) 'mode-line-fg)
    	  (slot-value (with-current-theme) 'mode-line-bg)))

  (defun get-bar-zone-color-string (amount &optional (med 20) (hi 50) (crit 90) reverse)
    (format nil "~a~a~a"
      	  (stumpwm:bar-zone-color amount med hi crit reverse)
      	  amount
      	  ;; Reset the color once we are done
      	  (ml-foreground-color
      	   (slot-value (with-current-theme) 'mode-line-fg))))

  (defun ml-fmt-background-color-by-range (message amount &optional (med 20) (hi 50) (crit 90) reverse)
    (flet ((past (n) (funcall (if reverse #'<= #'>=) amount n)))
      (let ((open-color (cond ((past crit) (ml-fmt-high-priority-bg))
  			    ((past hi) (ml-fmt-medium-priority-bg))
  			    ((past med) (ml-fmt-low-priority-bg))
  			    (t ""))))
        (format nil "~a~a~a~a"
  	      open-color
  	      (ml-foreground-color (slot-value (with-current-theme) 'black))
  	      message
  	      (mode-line-clear-changes)))))

  (defvar *show-mode-line-time* t)

  (defun trimmed-shell-command (command)
    (string-trim '(#\Space #\Newline #\Tab #\Linefeed #\Return)
    	       (run-shell-command command t)))

  (defun low-priority-message (msg)
    (format nil "~a~a~a"
    	  (light-bg)
    	  msg
    	  (mode-line-clear-changes)))

  (defun medium-priority-message (msg)
    (format nil "~a~a~a"
    	  (medium-bg)
    	  msg
    	  (mode-line-clear-changes)))

  (defun high-priority-message (msg)
    (format nil "~a~a~a"
    	  (dark-bg)
    	  msg
    	  (mode-line-clear-changes)))

  (defun error-message-bar (msg)
    (format nil " ^(:fg \"~A\")~A~A" (slot-value (with-current-theme) 'red) msg (mode-line-clear-changes)))

  (add-screen-mode-line-formatter #\M 'fmt-pretty-head-window-list)
  (defun fmt-pretty-head-window-list (ml)
    "Using *window-format*, return a 1 line list of the windows, space seperated."
    (format nil "~{~a~^~}"
  	  (let ((base-fmt
  		  (mapcar (lambda (w)
  			    (format-with-on-click-id 
  			     (let ((str (format-expand *window-formatters*
  						       ,*window-format*
  						       w)))
  			       (if (eq w (current-window))
  				   (stumpwm::fmt-highlight (format nil " ~a " str))
  				   (format nil "~a|" str)))
  			     :ml-on-click-focus-window
  			     (stumpwm::window-id w)))
  			  (stumpwm::sort1 (stumpwm::head-windows (stumpwm::mode-line-current-group ml)
  								 (stumpwm::mode-line-head ml))
  					  #'< :key #'window-number))))
  	    ;; Reparse strings and remove the | from the element before the active window
  	    (if (> (length base-fmt) 0)
  		(loop for win-fmt-el from 1 to (- (length base-fmt) 1)
  		      do (if (search "^r^(:on-click-end)" (nth win-fmt-el base-fmt))
  			     (let* ((pos (- win-fmt-el 1))
  				    (pos-str (nth pos base-fmt)))
  			       (setf (nth pos base-fmt)
  				     (cl-ppcre:regex-replace-all "\\|\\^\\(:on-click-end\\)"
  								 pos-str
  								 "\^\(\:on-click-end\)"))))))
  	    base-fmt)))

  (defun ml-email-data ()
    (flet ((notmuch-count
  	     (tags)
  	   (parse-integer (trimmed-shell-command (format nil "notmuch count ~A" tags)))))
      (let* ((tnatkinson-email-count (notmuch-count "tag:tnatkinson95 and tag:unread and not tag:promotions"))
  	   (natalieatkinson-email-count (notmuch-count "tag:natalienatkinson95 and tag:unread and not tag:promotions"))
  	   (work-email-count (notmuch-count "tag:work and tag:unread")))
        (list :tnatkinson tnatkinson-email-count
  	    :natalieatkinson natalieatkinson-email-count
  	    :work work-email-count))))

  (add-screen-mode-line-formatter #\E 'ml-email-fmt)
  (defun ml-email-fmt (ml)
    (let ((email-counts (ml-email-data)))
      (if (> (+ (getf email-counts :tnatkinson)
  	      (getf email-counts :natalieatkinson)
  	      (getf email-counts :work))
  	   0)
  	(medium-priority-message
  	 (format nil " ~a ~a ~a Emails "
  		 (get-bar-zone-color-string (getf  email-counts :tnatkinson)
  					    5 10 15)
  		 (get-bar-zone-color-string (getf  email-counts :natalieatkinson)
  					    5 10 15)
  		 (get-bar-zone-color-string (getf email-counts :workc) 5 10 15)))
  	"")))

  (defun ml-read-file-as-integer (path)
    (with-open-file (s path :direction :input)
      (parse-integer (string-trim '(#\Newline #\Space)
                                  (read-line s)))))

  (defun ml-read-file-as-string (path)
    (with-open-file (s path :direction :input)
      (string-trim '(#\Newline #\Space)
                   (read-line s))))

  (defun ml-hwmon-directories ()
    (directory "/sys/class/hwmon/hwmon*"))

  (defun ml-temperature-files (hwmon-dir)
    (directory (merge-pathnames "temp*_input" hwmon-dir)))

  (defun ml-read-temperatures ()
    (loop for hwmon in (ml-hwmon-directories)
          for name-path = (merge-pathnames "name" hwmon)
          for chip-name = (when (probe-file name-path)
                            (ml-read-file-as-string name-path))
          append
            (loop for temp-file in (ml-temperature-files hwmon)
                  for milli-c = (ml-read-file-as-integer temp-file)
                  collect
                    (list :chip chip-name
                          :sensor (pathname-name temp-file)
                          :celsius (/ milli-c 1000.0)))))

  (defun ml-get-hottest ()
    (let ((hottest (list :celsius 0)))
      (dolist (entry (ml-read-temperatures))
        (if (> (getf entry :celsius) (getf hottest :celsius))
  	  (setf hottest entry)))
      hottest))

  (add-screen-mode-line-formatter #\H 'ml-tempreture-fmt)
  (defun ml-tempreture-fmt (ml)
    (let ((hottest (ml-get-hottest)))
      (if (> (getf hottest :celsius) 50)
  	(medium-priority-message
  	 (format nil " ~a ~a "
  		 (get-bar-zone-color-string (getf hottest :celsius) 50 60 80)
  		 (getf hottest :sensor)))
  	"")))

  (defun ml-window-list-fmt ()
    (format nil "~A%M" (mode-line-clear-changes)))

  (defun get-mode-line-format ()
    (list
     (if (equal (getenv "GUIX_HOME_SYSTEM_FORMAT") "laptop")
         ;; This looks weird if we don't add a double space after the battery
         " %B  ")
     (if *show-mode-line-time* "^R %d ^r" "")
     (ml-email-fmt)
     (ml-window-list-fmt)))

  (defun init-window-number-fixes ()
  ;;; When windows are desroyed window numbers are not synced
  ;;; 2kays <https://github.com/2kays> posted a solution on
  ;;; the TipsAndTricks section of the wiki
  ;;; This will repack window numbers every time a window is killed
    (stumpwm:add-hook stumpwm:*destroy-window-hook*
                      #'(lambda (win) (stumpwm:repack-window-numbers))))

  (defun toggle-modeline-all-screens ()
    "We almost allways want to interact with the modeline on all screens"
    (mapcar (lambda (head)
  	    (toggle-mode-line (current-screen) head))
  	  (screen-heads (current-screen))))

  (defun reload-mode-line ()
    "This runs toggle-modeline-all-screens twice so that we get the settings refreshed"
    (toggle-modeline-all-screens)
    (toggle-modeline-all-screens))

  (defun init-mode-line ()
    (setf *window-format* " %n %10c ")  
    
    (set-bar-med-color)
    (set-bar-hi-color)
    (set-bar-crit-color)

    (setf *screen-mode-line-format*      
  	(list
  	 "%M^>%H%E"
  	 '(:eval (if *show-mode-line-time* "^R %d ^r" ""))
  	 '(:eval (if (equal (getenv "GUIX_HOME_SYSTEM_FORMAT") "laptop")
  		     ;; This looks weird if we don't add a double space after the battery
  		     " %B "))

  	 ;; '(:eval (multiple-value-bind
  	 ;; 		     (output error exit-code)
  	 ;; 		   (uiop:run-program "ping -c 1 8.8.8.8" :ignore-error-status t)
  	 ;; 		 (if (not (equal exit-code 0))
  	 ;; 		     (error-message-bar "Network Error"))))
  	 ))

    (init-window-number-fixes)

    (toggle-modeline-all-screens))

#+end_src
*** Run init
#+begin_src lisp
  (defvar *fonts* (list "-adobe-courier-normal-r-normal-*-14-*-*-*-m-*-*-*"
  		      ;; also bold
   		      "-*-helvetica-medium-r-normal-*-14-*-*-*-*-*-*-*"
   		      "-*-new century schoolbook-r-normal-*-*-*-*-*-*-*-*-*-*"
   		      "-*-times-*-*-*-*-*-*-*-*-*-*-*-*"))

  (defun init-system-themeing ()
    (set-system-themeing)
    (set-font (nth 0 *fonts*))

    (run-shell-command "feh --bg-fill --no-xinerama ~/.background.jpg")
    
    (setf stumpwm:*input-window-gravity* :center
  	stumpwm:*message-window-gravity* :center
        
  	stumpwm:*message-window-padding* 10
  	stumpwm:*message-window-y-padding* 10))


#+end_src

* User functions
#+begin_src lisp
  (defun make-percent-bar (percent &optional title)
    "Return a string that represents a percent bar"
    (format nil "~a~%^B~3d%^b [^[^7*~a^]]"
            title
    	  percent
    	  (stumpwm::bar (min 100 percent) 50 #\# #\:)))

  (defun reload-rc-clean ()
    "Restart Slynk and reload source.
     This is needed if Sly updates while StumpWM is running"
    (slynk:stop-server 1337)
    (loadrc)
    (toggle-modeline-all-screens)
    (toggle-modeline-all-screens))

#+end_src
* Commands
** Brightness
#+begin_src lisp
  (defun show-screen-brightness ()
    (stumpwm:message (make-percent-bar
  		    (parse-integer (run-shell-command "sudo brillo -G" t) :junk-allowed t)	    
  		    "Screen Brightness")))

  (defcommand screen-brightness-up () ()
    "Increase the brightness of the screen"
    (run-shell-command "sudo brillo -A 10")
    (show-screen-brightness))

  (defcommand screen-brightness-down () ()
    "Decrease the brightness of the screen"
    (run-shell-command "sudo brillo -U 10")
    (show-screen-brightness))  

  (defun show-keyboard-brightness ()
    (stumpwm:message (make-percent-bar
  		    (parse-integer (run-shell-command "sudo brillo -Gk" t) :junk-allowed t)
  		    "Keyboard Brightness")))

  (defcommand keyboard-brightness-up () ()
    "Increase the brightness of the keyboard"
    (run-shell-command "sudo brillo -kA 10")
    (show-keyboard-brightness))

  (defcommand keyboard-brightness-down () ()
    "Decrease the brightness of the keyboard"
    (run-shell-command "sudo brillo -kU 10")
    (show-keyboard-brightness))

#+end_src
** Screenshots
#+begin_src lisp
  (defun timestamp-string ()
    (local-time:format-timestring
     nil (local-time:now)
     :format '(:YEAR "-" (:MONTH 2) "-" :DAY "-" :SHORT-WEEKDAY "-" :HOUR12 "_" :MIN "_" :SEC "_" :AMPM)))

  (defun screenshot-path ()
      (format nil "~a/Pictures/Screenshots/~a.png"
  	    (getenv "HOME")
  	    (timestamp-string)))

  ;; Setup bindings for less common aplications which would be opened then closed
  (defcommand screenshot () ()
  	    "Take a screenshot and save it to screenshot directory"
  	    (run-shell-command (format nil "maim ~a"
  				       (screenshot-path))))

  (defcommand screenshot-select () ()
  	    "Select a area for a screenshot and save it to screenshot directory"
  	    (run-shell-command (format nil "maim --select ~a"
  				       (screenshot-path))))
#+end_src
** Volume
#+begin_src lisp
  (setf pamixer:*allow-boost* t)  

  (defun show-volume-bar ()
    "Display a stumpwm:message of the current volume"
    (stumpwm:message (make-percent-bar (pamixer:get-volume) "Volume")))

  (defcommand notify-volume-up () ()
    (run-commands "pamixer-volume-up")
    (show-volume-bar))

  (defcommand notify-volume-down () ()
    (run-commands "pamixer-volume-down")
    (show-volume-bar))

  (defcommand volume-control () ()
    "Start volume control"
    (run-or-raise "pavucontrol" '(:class "Pavucontrol")))
#+end_src
** Theme
#+begin_src lisp
  (defcommand toggle-theme () ()
    "Toggle the system theme"
    (toggle-system-themeing))
#+end_src
** System
#+begin_src lisp
  ;;; Shutdown and Reboot
  (defcommand shutdown (confirm) ((:y-or-n "Confirm Shutdown "))
    "Ask for the user to confirm before shutting down."
    (if confirm
        (run-shell-command "sudo shutdown")))

  (defcommand reboot (confirm) ((:y-or-n "Confirm Reboot "))
    "Ask for the user to confirm before rebooting."
    (if confirm
        (run-shell-command "sudo reboot")))

  (defcommand reload-init (confirm) ((:y-or-n "Confirm Reloading init file "))
    "Ask for the user to confirm before reloading init file."
    (if confirm
        (reload-init)))

  (defcommand reload-mode-line () ()
    "Runs reload-mode-line. This allows the themeing etc to be changed"
    (reload-mode-line))
#+end_src
** Misc
#+begin_src lisp
  (defcommand user-switch-to-screen (screen-num) ((:number "Screen Number: "))
    "Only works when there is a currently open window on the screen"
    (select-window-by-number (window-number
  			    (car (head-windows (current-group)
  					       (nth screen-num (group-heads (current-group)))))))
    (group-wake-up (current-group)))
#+end_src
* Keybindings
#+begin_src lisp
  (defun init-keybindings ()
    (set-prefix-key (kbd "C-t")))
#+end_src
** Keybinding Macros
#+begin_src lisp
  (defmacro make-program-binding (program-name window-class &optional alias)
    "Create run-or-raise and run-or-pull commands for program-name
  window-class is the windows-class
  Also add keybinding to the commands. 
  C-keybinding r calls run-or-raise
  C-keybinding p calls run-or-pull
  C-keybinding n creates a new instance of the program"
    (if (not alias)
        (setf alias program-name))
    `(progn
       (defvar ,(intern (format nil "*~a-map*" alias)) nil)

       (defcommand ,(intern (format nil "~a" alias)) () () (run-shell-command ,program-name))
       
       (defcommand ,(intern (format nil "run-or-raise-~a" alias)) () ()
         (run-or-raise ,program-name '(:class ,window-class)))
       
       (defcommand ,(intern (format nil "run-or-pull-~a" alias)) () ()
         (run-or-pull ,program-name '(:class ,window-class)))
       
       (stumpwm::fill-keymap ,(intern (format nil "*~a-map*" alias))
  			   (kbd "p") ,(format nil "run-or-pull-~a" alias)
  			   (kbd "r") ,(format nil "run-or-raise-~a" alias)
  			   (kbd "n") ,(format nil "~a" alias))))
#+end_src
** Program Bindings
#+begin_src lisp
  (defun init-program-binding ()
    (make-program-binding "firefox" "Firefox")

    (make-program-binding "alacritty" "Alacritty")

    (make-program-binding "emacs" "Emacs" "emacs")

    (make-program-binding "keepassxc" "keepassxc")

    (make-program-binding "steam" "steam")

    (make-program-binding "firefox -P work --class firefox-work" "firefox-work" "firefox-work")

    (make-program-binding "firefox -P media --class firefox-media" "firefox-media" "firefox-media"))

#+end_src
** Keymaps
*** System Map
#+begin_src lisp
   ;;; System Command Keymap
  (defparameter *screenshot-map*
    (let ((m (make-sparse-keymap)))
      (define-key m (kbd "f") "screenshot")
      (define-key m (kbd "s") "screenshot-select")
      m))

  (defparameter *theme-map*
    (let ((m (make-sparse-keymap)))
      (define-key m (kbd "t") "toggle-theme")
      (define-key m (kbd "m") "reload-mode-line")
      m))


  (defparameter *power-map*
    (let ((m (make-sparse-keymap)))
      (define-key m (kbd "p") "shutdown")
      (define-key m (kbd "r") "reboot")
      m)) 

  (defparameter *system-map*
    (let ((m (make-sparse-keymap)))
      (define-key m (kbd "s") '*screenshot-map*)
      (define-key m (kbd "t") '*theme-map*)
      (define-key m (kbd "p") '*power-map*)
      (define-key m (kbd "r") "reload-rc-clean")
      (define-key m (kbd "v") "volume-control")
      m))
#+end_src
*** Program Map
#+begin_src lisp
  (defparameter *program-map*
    (let ((m (make-sparse-keymap)))
      (define-key m (kbd "f") '|*firefox-map*|)
      (define-key m (kbd "w") '|*firefox-work-map*|)
      (define-key m (kbd "m") '|*firefox-media-map*|)
      (define-key m (kbd "e") '|*emacs-map*|)
      (define-key m (kbd "c") '|*alacritty-map*|)
      (define-key m (kbd "p") '|*keepassxc-map*|)
      (define-key m (kbd "s") '|*steam-map*|)
      m))
#+end_src
*** Root Map
#+begin_src lisp
  (defun init-root-map ()
    (define-key *root-map* (kbd "0") "remove")
    (define-key *root-map* (kbd "1") "only")
    (define-key *root-map* (kbd "2") "vsplit")
    (define-key *root-map* (kbd "3") "hsplit")

    (define-key *root-map* (kbd "F1") "user-switch-to-screen 2")
    (define-key *root-map* (kbd "F2") "user-switch-to-screen 1")
    (define-key *root-map* (kbd "F3") "user-switch-to-screen 0")


    (define-key *root-map* (kbd "p") '*program-map*)
    (define-key *root-map* (kbd "s") '*system-map*))
#+end_src
*** Top Map
#+begin_src lisp
  (defun init-top-map ()
    (define-key *top-map* (kbd "XF86AudioRaiseVolume") "notify-volume-up")
    (define-key *top-map* (kbd "XF86AudioLowerVolume") "notify-volume-down")
    (define-key *top-map* (kbd "XF86AudioMute") "pamixer-toggle-mute")

    (define-key *top-map* (kbd "XF86MonBrightnessUp") "screen-brightness-up")
    (define-key *top-map* (kbd "XF86MonBrightnessDown") "screen-brightness-down")

    (define-key *top-map* (kbd "XF86KbdBrightnessUp") "keyboard-brightness-up")
    (define-key *top-map* (kbd "XF86KbdBrightnessDown") "keyboard-brightness-down"))

#+end_src
* Final Actions
#+begin_src lisp
  (defun run-all-inits ()
    ;; Themes
    (init-system-themeing)
    (init-mode-line)

    ;; Keymaps
    (init-keybindings)
    (init-program-binding)
    (init-root-map)
    (init-top-map)
    
    (run-shell-command "dex -a -s $XDG_CONFIG_HOME/autostart/"))

  (run-all-inits)
#+end_src
